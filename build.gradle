buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {

        def prop = new Properties()
        prop.load(it)
        ext.config = new ConfigSlurper().parse prop
}

group = "MineColoniesMod"
version = "${config.minecolonies_major}.${config.minecolonies_minor}.${config.minecolonies_build}"

minecraft {
        version = config.minecraft_version + "-" + config.forge_version
        assetDir = "run/assets"
		replace "@VERSION@", project.version
}

task incrementBuildNumber() {
	dependsOn "reobf"
	doLast {
		config.minecolonies_build = (config.minecolonies_build.toString().toInteger()) + 1

		configFile.withWriter {
				config.toProperties().store(it, "")
		}
    }
}
tasks.build.dependsOn "incrementBuildNumber"

processResources
{
        // this will ensure that this task is redone when the versions change.
        inputs.property "version", project.version
        inputs.property "mcversion", project.minecraft.version

        // replace stuff in mcmod.info, nothing else
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'

            // replace version and mcversion
            expand 'version': project.version, 'mcversion': project.minecraft.version
        }

        // copy everything else, thats not the mcmod.info
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }
}

repositories {
        mavenCentral()
}

jar {
        appendix = 'universal'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

task deobfJar(type: Jar) {
        from sourceSets.main.output
        appendix = 'deobf'
}

//The *-universal-* is the right one!
artifacts {
        archives sourceJar
        archives deobfJar
}